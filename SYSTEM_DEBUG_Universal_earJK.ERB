;FileName_SYSTEM_DEBUG_Universal_eraH.ERB -------------------------------- Rev1.04
;デバッグモード起動: @DEBUG_MENU_U
;Usage: Modify SHOP File to add "CALL DEBUG_MENU_U"
;
;Modify by Tsubasa (2015/5/28 4th Version)
;
;Original File: eraToho SYSTEM_DEBUG.ERB
;=============================================================================
;キャラリスト表示用の命令実装群
;=============================================================================
;-------------------------------------------------
;リストのキャラデータをセットする関数
;-------------------------------------------------
;CALLF SET_LIST, [リスト位置], [キャラ番号]
@SET_LIST(ARG, ARG:1)
#FUNCTION
RETURNF LIST_DATA("SET", ARG, ARG:1)

;-------------------------------------------------
;リストのキャラデータをセットする関数
;-------------------------------------------------
;[変数等] = GET_LIST([リスト位置])
@GET_LIST(ARG)
#FUNCTION
RETURNF LIST_DATA("GET", ARG)

;-------------------------------------------------
;内部で使用される関数
;-------------------------------------------------
;直接これを呼び出す必要はない
@LIST_DATA(ARGS, ARG, ARG:1)
#FUNCTION
IF ARGS == "SET"
	LOCAL:ARG = ARG:1
	RETURNF ARG:1
ELSEIF ARGS == "GET"
	RETURNF LOCAL:ARG
ELSEIF ARGS == "CLEAR"
	VARSET LOCAL, 0
	RETURNF -1
ELSE
	RETURNF -1
ENDIF

;-------------------------------------------------
;リストをクリアする関数
;-------------------------------------------------
;CALLF CLEAR_LIST
@CLEAR_LIST
#FUNCTION
RETURNF LIST_DATA("CLEAR")

;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;デバッグモード用の特殊指令
;≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡≡
;以下焼き芋弁当パッチ用のデバッグモードを起動しているときに使える特殊な処理をここで一括で管理
;大きく分けて調教中に使えるものとメイン菜單(SHOP)が開いているときに使用できるものに分かれる
;=============================================================================
;調教中に使用可能な指令
;=============================================================================
@DEBUG_MENU_U
DRAWLINE
PRINTFORML {DAY}日目
IF TARGET >= 0
	PRINTFORM %CALLNAME:TARGET% 選択中　
ENDIF
PRINTFORML (残り{MONEY}圓)
DRAWLINE
PRINTL 何をしますか？
PRINTL 
PRINTLC [ 0] - 角色編集
PRINTLC [ 1] - 所持金変更
PRINTLC [ 2] - FLAG操作
PRINTLC [ 3] - 角色全回復
PRINTLC [ 4] - ITEM編集
PRINTLC [99] - NEXT DAY
PRINTLC [100] - 戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 0
	LOCAL = MASTER
	LOCAL:1 = TARGET
	LOCAL:2 = ASSI
	CALL EDIT_CHARA
	MASTER = LOCAL
	TARGET = LOCAL:1
	ASSI = LOCAL:2
ELSEIF RESULT == 1
	CALL EDIT_MONEY
ELSEIF RESULT == 2
	CALL EDIT_FLAG
ELSEIF RESULT == 3
	CALL EDIT_HEAL_ALL
ELSEIF RESULT == 4
	CALL EDIT_ITEM
ELSEIF RESULT == 99
	CALL NEXTDAY
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART

;--------------------------------------------------
;所持金変更処理
;--------------------------------------------------
@EDIT_MONEY
PRINTFORML (残り{MONEY:0}圓 or 残り{MONEY:1}圓 or 残り{MONEY:2}圓)
DRAWLINE
FOR LOCAL, 0, 3
	PRINTFORMLC [{LOCAL, 3}]MONEY:{LOCAL, 3}  = {MONEY:LOCAL}
NEXT
PRINTLC [200]戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 3
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML MONEY:{LOCAL} の変更(現在{MONEY:LOCAL})
	PRINTFORML 数値を入力[0-99999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 99999999
		RESTART
	MONEY:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
;キャラ全快
;--------------------------------------------------
@EDIT_HEAL_ALL
REPEAT CHARANUM
	BASE:COUNT:0 = MAXBASE:COUNT:0
	BASE:COUNT:1 = MAXBASE:COUNT:1
REND
PRINTW 全員の体力・気力が回復しました

;--------------------------------------------------
;キャラ書き換え
;--------------------------------------------------
@EDIT_CHARA
;前後のキャラを表示できるか判定
LOCAL:1 = -1
LOCAL:2 = -1
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == TARGET
		CONTINUE
	IF LOCAL < TARGET
		LOCAL:1 = LOCAL
	ELSE
		LOCAL:2 = LOCAL
		BREAK
	ENDIF
NEXT

DRAWLINE
PRINTFORML \@(TARGET < 0) ? 対象を選択してください # 現在「%CALLNAME:TARGET%」を選択中\@
DRAWLINE
PRINTFORMLC [  0] - \@(TARGET >= 0) ? 対象変更 # 対象選択\@
IF TARGET >= 0
;	PRINTLC [  1] - 対象情報
	PRINTLC [  2] - ABL編集
	PRINTLC [  3] - EXP編集
	PRINTLC [  4] - MARK編集
	PRINTLC [  5] - TALENT編集
	PRINTLC [  6] - JUEL編集
	PRINTLC [  7] - CFLAG操作
	PRINTLC [  8] - PALAM編集
	PRINTLC [  9] - BASE編集
	PRINTLC [  10] - MAXBASE編集
	PRINTLC [  11] - 関係編集
ENDIF
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL:1 < 0) ? %" " * 16% # [1011]%CALLNAME:(LOCAL:1)%を編集\@
PRINTFORMLC [1000]戻る
PRINTFORMLC \@(LOCAL:2 < 0) ? %" " * 16% # [1019]%CALLNAME:(LOCAL:2)%を編集\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1011 && LOCAL:1 >= 0
	TARGET = LOCAL:1
ELSEIF RESULT == 1019 && LOCAL:2 >= 0
	TARGET = LOCAL:2
ELSEIF RESULT == 0
	CALL SELECT_TARGET
ELSEIF TARGET < 0
	PRINTW 対象を選択してください。
;ELSEIF RESULT == 1
;	CALL PRINT_STATE(TARGET)
ELSEIF RESULT == 2
	CALL EDIT_ABL
ELSEIF RESULT == 3
	CALL EDIT_EXP
ELSEIF RESULT == 4
	CALL EDIT_MARK
ELSEIF RESULT == 5
	CALL EDIT_TALENT
ELSEIF RESULT == 6
	CALL EDIT_JUEL
ELSEIF RESULT == 7
	CALL EDIT_CFLAG
ELSEIF RESULT == 8
	CALL EDIT_PALAM
ELSEIF RESULT == 9
	CALL EDIT_BASE
ELSEIF RESULT == 10
	CALL EDIT_MAX_BASE
ELSEIF RESULT == 11
	CALL EDIT_AAA(TARGET)
ENDIF
RESTART

@SELECT_TARGET
;表示させるキャラを抽出（LOCAL:2に人数）
VARSET LOCAL, 0
CALLF CLEAR_LIST
LOCAL:1 = CHARANUM / 20

$PRINT_LIST
DRAWLINE
SIF TARGET >= 0
	PRINTFORML 現在の選択角色:%CALLNAME:TARGET%
PRINTFORML 誰を対象にしますか？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT CHARANUM
	SIF COUNT >= LOCAL * 20 && COUNT < (LOCAL+1) * 20
		PRINTFORML [{COUNT}]%NAME:COUNT%
REND
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 16% # [1009]次のページ\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF

TARGET = RESULT
PRINTFORML %CALLNAME:TARGET%を対象に選択しました

;--------------------------------------------------
; PALAM操作
;--------------------------------------------------
@EDIT_PALAM
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%PALAMNAME:LOCAL% ({PALAM:LOCAL}) 
NEXT
PRINTLC [199]パラメータリセット
PRINTLC [200]戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 199
	VARSET PALAM, 0
	PRINTW パラメータを全てリセットしました
	RESTART
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		GOTO INPUT_LOOP
	DRAWLINE
	PRINTFORML %PALAMNAME:LOCAL%の変更(現在{PALAM:LOCAL})
	PRINTFORML 数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	PALAM:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; BASE操作
;--------------------------------------------------
@EDIT_BASE
DRAWLINE
FOR LOCAL, 0, 100
	PRINTFORMLC [{LOCAL, 3}] \@ (STRLENS(BASENAME:LOCAL) == 0) ? BASE:{LOCAL, 3} # %BASENAME:LOCAL%\@ = {BASE:LOCAL}
NEXT
PRINTLC [199]BASEリセット
PRINTLC [200]戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 199
	VARSET BASE, 0
	PRINTW BASEを全てリセットしました
	RESTART
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	SIF STRLENS(BASENAME:LOCAL) == 0
		GOTO INPUT_LOOP
	DRAWLINE
	PRINTFORML \@ (STRLENS(BASENAME:LOCAL) == 0) ? BASE:{LOCAL} # %BASENAME:LOCAL%\@ の変更(現在{BASE:LOCAL})
;	PRINTFORML %BASENAME:LOCAL%の変更(現在{BASE:LOCAL})
	PRINTFORML 数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	BASE:LOCAL = RESULT
ENDIF
RESTART


;--------------------------------------------------
; ABL操作
;--------------------------------------------------
;ABL変更
@EDIT_ABL
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(ABLNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%ABLNAME:LOCAL%LV ({ABL:LOCAL})  
NEXT
PRINTL 
PRINTLC [200]戻る
PRINTLC [201]ABLを全て10
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(ABLNAME:LOCAL) == 0
		CONTINUE
	ABL:LOCAL = 10
	NEXT
	PRINTW ABLを全て10しました
	RESTART
SIF RESULT < 0 || RESULT > 107
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML %ABLNAME:LOCAL%LVの変更(現在{ABL:LOCAL})
	PRINTFORML 数値を入力[0-99]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 99
		RESTART
	ABL:LOCAL = RESULT
ENDIF

RESTART

;--------------------------------------------------
;素質編集
;--------------------------------------------------
@EDIT_TALENT
LOCAL = 0
LOCAL:1 = 4
;CALLF CLEAR_LIST
;表示させる素質を抽出
FOR LOCAL, 0, 220
	SIF STRLENS(TALENTNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%TALENTNAME:LOCAL% ({TALENT:LOCAL})  
NEXT
PRINTL
DRAWLINE
PRINTFORML どの素質を変更しますか？ ＜page.{LOCAL+1}＞
DRAWLINE
PRINTLC [9999]戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 9999
	RETURN 0
ENDIF
SIF STRLENS(TALENTNAME:RESULT) == 0
	GOTO INPUT_LOOP
LOCAL:1 = RESULT
PRINTFORML %TALENTNAME:(RESULT)%の変更
PRINTFORML 数値を入力（-9 - 49, CSVファイルを参照してください）
INPUT
SIF RESULT < -9 || RESULT > 49
	RESTART
TALENT:(LOCAL:1) = RESULT
RESTART


;--------------------------------------------------
; MARK操作
;--------------------------------------------------
;MARK変更
@EDIT_MARK
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(MARKNAME:LOCAL) == 0
		CONTINUE
	PRINTFORML [{LOCAL, 3}]%MARKNAME:LOCAL% ({MARK:LOCAL})
NEXT
PRINTL [100]戻る
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
SIF STRLENS(MARKNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %MARKNAME:LOCAL%LVの変更(現在{MARK:LOCAL})
PRINTFORML 数値を入力[0-3]（無効な数値でキャンセル）
INPUT
SIF RESULT < 0 || RESULT >3
	RESTART
MARK:LOCAL = RESULT

RESTART

;--------------------------------------------------
; EXP操作
;--------------------------------------------------
;EXP変更
@EDIT_EXP
DRAWLINE
FOR LOCAL:1, 0, 100
	LOCAL = LOCAL:1
	SIF STRLENS(EXPNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%EXPNAME:LOCAL% ({EXP:LOCAL})　
NEXT
PRINTLC [200]戻る
PRINTLC [201]EXPを全て1999
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(EXPNAME:LOCAL) == 0
		CONTINUE
	EXP:LOCAL = 1999
	NEXT
	PRINTW EXPを全てMAXしました
	RESTART
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSE
LOCAL = RESULT
SIF STRLENS(EXPNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %EXPNAME:LOCAL%の変更(現在{EXP:LOCAL})
PRINTFORML 数値を入力（負の数値でキャンセル）
INPUT
SIF RESULT < 0
	RESTART
EXP:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; JUEL操作
;--------------------------------------------------
;JUEL変更
@EDIT_JUEL
DRAWLINE
FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	PRINTFORMLC [{LOCAL, 3}]%PALAMNAME:LOCAL%の珠({JUEL:LOCAL}) 
NEXT
PRINTLC [200]戻る
PRINTLC [201]JUELを全て9999999
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 201
	FOR LOCAL, 0, 100
	SIF STRLENS(PALAMNAME:LOCAL) == 0
		CONTINUE
	JUEL:LOCAL = 9999999
	NEXT
	PRINTW JUELを全てMAXしました
	RESTART
ELSEIF RESULT < 0 || RESULT > 100
	GOTO INPUT_LOOP
ELSE
LOCAL = RESULT
SIF STRLENS(PALAMNAME:LOCAL) == 0
	GOTO INPUT_LOOP
DRAWLINE
PRINTFORML %PALAMNAME:LOCAL%の珠の変更(現在{JUEL:LOCAL})
PRINTFORML 数値を入力（負の数値でキャンセル）
INPUT
SIF RESULT < 0
	RESTART
JUEL:LOCAL = RESULT
ENDIF
RESTART


;--------------------------------------------------
; CFLAG操作
;--------------------------------------------------
@EDIT_CFLAG
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 現在立っているCFLAG (MAX = 1000)
PRINTFORML どれを変更しますか？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] \@ (STRLENS(CFLAGNAME:COUNT) == 0) ? CFLAG:{COUNT, 3} # %CFLAGNAME:COUNT%\@ = {CFLAG:COUNT}
;	PRINTFORMLC [{COUNT, 3}] TFLAG:{COUNT, 3} = {CFLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= 19) ? %" " * 16% # [1009]次のページ\@
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < 19
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML \@ (STRLENS(CFLAGNAME:RESULT) == 0) ? CFLAG:{RESULT} # %CFLAGNAME:RESULT%\@ の変更(現在{CFLAG:RESULT})
;PRINTFORML CFLAG:{RESULT}の変更(現在{CFLAG:RESULT})
PRINTFORML 数値を入力（数値-999でキャンセル）
INPUT
SIF RESULT == -999
	RESTART
CFLAG:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;TFLAG編集
;--------------------------------------------------
@EDIT_TFLAG
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 現在立っているTFLAG
PRINTFORML どれを変更しますか？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] TFLAG:{COUNT, 3} = {TFLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= 19) ? %" " * 16% # [1009]次のページ\@
PRINTL [1010]調教フラグ全リセット(要注意)
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < 19
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1010
	VARSET TFLAG, 0
	PRINTW 調教フラグを全てリセットしました
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML TFLAG:{RESULT}の変更(現在{TFLAG:RESULT})
PRINTFORML 数値を入力（負の数値でキャンセル）
INPUT
SIF RESULT < 0
	RESTART
TFLAG:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;FLAG編集
;--------------------------------------------------
;説明付与
@DEFINE_FLAG, ARG
IF STRLENS(FLAGNAME:ARG) > 0
	PRINTS FLAGNAME:ARG
ELSE
	PRINT ――
ENDIF

@EDIT_FLAG
VARSET LOCAL, 0
LOCAL:1 = 199
$PRINT_LIST
DRAWLINE
PRINTFORML 現在立っているFLAG
PRINTFORML どれを変更しますか？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 10000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 4}] \@ (STRLENS(FLAGNAME:COUNT) == 0) ? FLAG:{COUNT, 4} # %FLAGNAME:COUNT%\@ = {FLAG:COUNT}
;	PRINTFORMLC [{COUNT, 4}] FLAG:{COUNT, 4} = {FLAG:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 17% # [10001]前のページ\@
PRINTLC [10000]戻る
PRINTFORMLC \@(LOCAL >= LOCAL:1) ? %" " * 17% # [10009]次のページ\@
PRINTL 
PRINTFORMLC \@(LOCAL/10 <= 0) ? %" " * 17% # [10011]10ページ前\@
PRINTFORMLC %" " * 17%
PRINTFORMLC \@(LOCAL/10 >= LOCAL:1/10) ? %" " * 17% # [10019]10ページ先\@
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 10000
	RETURN 0
ELSEIF RESULT == 10001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT == 10011 && LOCAL/10 > 0
	LOCAL -= 10
	GOTO PRINT_LIST
ELSEIF RESULT == 10019 && LOCAL/10 < LOCAL:1/10
	LOCAL += 10
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 10000
	GOTO INPUT_LOOP
ENDIF
LOCAL:2 = RESULT
DRAWLINE
PRINTFORM FLAG:{LOCAL:2}(
CALL DEFINE_FLAG, LOCAL:2
PRINTFORML )の変更(現在{FLAG:(LOCAL:2)})
PRINTFORML 数値を入力（負の数値でキャンセル）
INPUT
SIF RESULT < 0
	RESTART
FLAG:(LOCAL:2) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
; MAXBASE操作
;--------------------------------------------------
@EDIT_MAX_BASE
DRAWLINE
FOR LOCAL, 0, 100
	PRINTFORMLC [{LOCAL, 3}] \@ (STRLENS(BASENAME:LOCAL) == 0) ? MAXBASE:{LOCAL, 3} # %BASENAME:LOCAL%\@ = {MAXBASE:LOCAL}
;	PRINTFORMLC [{LOCAL, 3}]%BASENAME:LOCAL% ({MAXBASE:LOCAL}) 
NEXT
PRINTLC [200]戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT < 0 || RESULT > 99
	GOTO INPUT_LOOP
ELSE
	LOCAL = RESULT
	DRAWLINE
	PRINTFORML \@ (STRLENS(BASENAME:LOCAL) == 0) ? MAXBASE:{LOCAL} # %BASENAME:LOCAL%\@ の上限変更(現在{MAXBASE:LOCAL})
	PRINTFORML 数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	MAXBASE:LOCAL = RESULT
ENDIF
RESTART

;--------------------------------------------------
; 関係操作
;--------------------------------------------------
@EDIT_AAA(TARGET)
DRAWLINE
LOCAL:1 = -1
LOCAL:2 = -1
FOR LOCAL, 0, CHARANUM
	SIF LOCAL == TARGET
		CONTINUE
	IF LOCAL < TARGET
		LOCAL:1 = LOCAL
	ELSE
		LOCAL:2 = LOCAL
		BREAK
	ENDIF
NEXT

VARSET LOCAL, 0
CALLF CLEAR_LIST
LOCAL:1 = CHARANUM / 20
DRAWLINE
SIF TARGET >= 0
	PRINTFORML 現在の関係択角色:%CALLNAME:TARGET%
PRINTFORML 誰を関係対象にしますか？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT CHARANUM
		PRINTFORML [{COUNT}]%NAME:COUNT%
REND
DRAWLINE
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < LOCAL:1
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
LOCAL:2 = TARGET
$PRINT_LIST
PRINTFORML %CALLNAME:(LOCAL:1)%を%CALLNAME:(LOCAL:2)%関係対象に選択しました
DRAWLINE
PRINTFORML %CALLNAME:(LOCAL:1)% を %CALLNAME:(LOCAL:2)%関係
DRAWLINE
	PRINTFORM  [1]肉欲:
	PRINTFORM {TA:(LOCAL:1):(LOCAL:2):0,6}
	PRINTFORM  [2]愛情:
	PRINTFORM {TA:(LOCAL:1):(LOCAL:2):1,6}
	PRINTFORM  [3]服従:
	PRINTFORM {TA:(LOCAL:1):(LOCAL:2):2,6}
	PRINTFORM  [4]好意:
	PRINTFORM {TA:(LOCAL:1):(LOCAL:2):3,6}
	PRINTFORM  [5]欲情:
	PRINTFORM {TA:(LOCAL:1):(LOCAL:2):4,6}
	PRINTFORM  [6]敵対:
	PRINTFORML {TA:(LOCAL:1):(LOCAL:2):6,6}
DRAWLINE
PRINTLC [200]戻る
PRINTL 
$INPUT_LOOP
INPUT
IF RESULT == 200
	RETURN 0
ELSEIF RESULT == 1
	PRINTFORML 肉欲数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	TA:(LOCAL:1):(LOCAL:2):0 = RESULT
ELSEIF RESULT == 2
	PRINTFORML 愛情数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	TA:(LOCAL:1):(LOCAL:2):1 = RESULT
ELSEIF RESULT == 3
	PRINTFORML 服従数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	TA:(LOCAL:1):(LOCAL:2):2 = RESULT
ELSEIF RESULT == 4
	PRINTFORML 好意数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	TA:(LOCAL:1):(LOCAL:2):3 = RESULT
ELSEIF RESULT == 5
	PRINTFORML 欲情数値を入力[0-9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < 0 || RESULT > 9999999
		RESTART
	TA:(LOCAL:1):(LOCAL:2):4 = RESULT
ELSEIF RESULT == 6
	PRINTFORML 敵対数値を入力[-9999999~9999999]（無効な数値でキャンセル）
	INPUT
	SIF RESULT < -9999999 || RESULT > 9999999
		RESTART
	TA:(LOCAL:1):(LOCAL:2):6 = RESULT
ENDIF
GOTO PRINT_LIST


;--------------------------------------------------
; ITEM操作
;--------------------------------------------------
;ITEM変更
@EDIT_ITEM
DRAWLINE
LOCAL = 0
$PRINT_LIST
DRAWLINE
PRINTFORML 現在立っているITEM (MAX = 1000)
PRINTFORML どれを変更しますか？ ＜page.{LOCAL+1}＞
DRAWLINE
REPEAT 1000
	SIF COUNT < LOCAL * 50 || COUNT >= (LOCAL + 1) * 50
		CONTINUE
	PRINTFORMLC [{COUNT, 3}] \@ (STRLENS(ITEMNAME:COUNT) == 0) ? ITEM:{COUNT, 3} # %ITEMNAME:COUNT%\@ = {ITEM:COUNT}
;	PRINTFORMLC [{COUNT, 3}] ITEM:{COUNT, 3} = {ITEM:COUNT}
REND
PRINTL 
DRAWLINE
PRINTFORMLC \@(LOCAL <= 0) ? %" " * 16% # [1001]前のページ\@
PRINTLC [1000]戻る
PRINTFORMLC \@(LOCAL >= 19) ? %" " * 16% # [1009]次のページ\@
$INPUT_LOOP
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001 && LOCAL > 0
	LOCAL -= 1
	GOTO PRINT_LIST
ELSEIF RESULT == 1009 && LOCAL < 19
	LOCAL += 1
	GOTO PRINT_LIST
ELSEIF RESULT < 0 || RESULT > 1000
	GOTO INPUT_LOOP
ENDIF
LOCAL:1 = RESULT
DRAWLINE
PRINTFORML \@ (STRLENS(ITEMNAME:RESULT) == 0) ? ITEM:{RESULT} # %ITEMNAME:RESULT%\@ の変更(現在{ITEM:RESULT})
;PRINTFORML ITEM:{RESULT}の変更(現在{ITEM:RESULT})
PRINTFORML 数値を入力（数値-999でキャンセル）
INPUT
SIF RESULT == -999
	RESTART
ITEM:(LOCAL:1) = RESULT
GOTO PRINT_LIST

;--------------------------------------------------
;NEXTDAY
;--------------------------------------------------
@NEXTDAY
DAY += 1
BEGIN AFTERTRAIN
